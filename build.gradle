import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.7.0'
        classpath 'org.javamodularity:moduleplugin:1.8.10'
        classpath 'org.beryx:badass-jlink-plugin:2.25.0'
    }
}

plugins {
    id 'java-library'
    id 'application'
    id 'com.google.osdetector'           version '1.7.0'
    id 'org.javamodularity.moduleplugin' version '1.8.10'
    id 'org.beryx.jlink'                 version '2.25.0'
    id 'net.nemerosa.versioning'         version '2.15.1'
}

normalization {
    runtimeClasspath {
        ignore('/META-INF/MANIFEST.MF')
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

Date buildTimeAndDate = new Date()
ext {
    buildDate       = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime       = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
    platform        = osdetector.os == 'osx' ? osdetector.arch == 'aarch_64' ? 'mac-aarch64' : 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os == 'linux' ? osdetector.arch == 'aarch_64' ? 'linux-aarch64' : 'linux' : osdetector.os
    ciOssrhUsername = System.getenv('OSSRH_USERNAME')
    ciOssrhPassword = System.getenv('OSSRH_PASSWORD')
    ciGHUser        = System.getenv('GH_USER')
    ciGHToken       = System.getenv('GH_TOKEN')
    gpgkey          = System.getenv("GPG_PRIVATE_KEY")
    gpgpassphrase   = System.getenv("PASSPHRASE")
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'io.foojay.api:discoclient:2.0.29'
    implementation 'eu.hansolo:jdktools:17.0.17'
    implementation 'com.google.gradle:osdetector-gradle-plugin:1.7.0'
    implementation 'com.dustinredmond.fxtrayicon:FXTrayIcon:3.2.0'
    implementation 'io.github.1tchy.java9modular.org.apache.commons:commons-compress:1.21.0'
    implementation 'org.tukaani:xz:1.9'
    implementation "org.openjfx:javafx-base:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-graphics:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-controls:${javafxVersion}:${platform}"
}


mainClassName = '$moduleName/eu.hansolo.fx.jdkmon.Launcher'
ext.moduleName = 'eu.hansolo.fx.jdkmon'
mainClassName  = 'eu.hansolo.fx.jdkmon.Launcher'
description    = 'JDKMon is a little tool written in JavaFX that will help you to keep track of updates for your installed distributions of OpenJDK'

jar {

    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.runtimeClasspath.collect {  it.isDirectory() ? it : zipTree(it)  }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    manifest {
        attributes(
                'Built-By'              : System.properties['user.name'],
                'Created-By'            : System.properties['java.version'] + " (" + System.properties['java.vendor'] + " " + System.properties['java.vm.version'] + ")",
                'Build-Date'            : project.buildDate,
                'Build-Time'            : project.buildTime,
                'Build-Revision'        : versioning.info.commit,
                'Specification-Title'   : project.name,
                'Specification-Version' : project.version,
                'Implementation-Title'  : project.name,
                'Implementation-Version': project.version,
                'Bundle-Name'           : project.name,
                'Bundle-License'        : 'https://www.apache.org/licenses/LICENSE-2.0;description=Apache License Version 2.0;link=https://spdx.org/licenses/Apache-2.0.html',
                'Bundle-Description'    : description,
                'Bundle-SymbolicName'   : 'eu.hansolo.fx.jdkmon',
                'Class-Path'            : '${project.name}-${project.version}.jar',
                'Main-Class'            : 'eu.hansolo.fx.jdkmon.Launcher'
        )
    }
}

/*
tasks.register('uberJar', Jar) {
    archiveClassifier = 'uber'

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}
*/

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'eu.hansolo.fx.jdkmon'
    }
}
/*
tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}
tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}
tasks.withType(JavaExec) {
    jvmArgs += "--enable-preview"
}
*/

// start the from gradle
task Main(type: JavaExec) {
    mainClass = "eu.hansolo.fx.jdkmon.Launcher"
    classpath = sourceSets.main.runtimeClasspath
}

// create properties file including the version
task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir//classes/java/main/eu/hansolo/fx/jdkmon/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

// Fix problems with loading resources
sourceSets {
    main {
        output.setResourcesDir(java.classesDirectory)
    }
}

run {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--module', mainClassName
        ]
        classpath = files()
    }
}